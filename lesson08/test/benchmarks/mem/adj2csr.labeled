@rand(seq: ptr<int>, max: int): int {
.rand.b0:
  a: int = const 25214903917;
  c: int = const 11;
  m: int = const 281474976710656;
  x: int = load seq;
  ax: int = mul a x;
  axpc: int = add ax c;
  next: int = div axpc m;
  next: int = mul next m;
  next: int = sub axpc next;
  store seq next;
  val: int = div next max;
  val: int = mul val max;
  val: int = sub next val;
  ret val;
}
@randarray(size: int, rng: ptr<int>): ptr<int> {
.randarray.b0:
  arr: ptr<int> = alloc size;
  i: int = const 0;
  max: int = const 2;
  one: int = const 1;
  zero: int = const 0;
.loop:
  cond: bool = lt i size;
  br cond .body .done;
.body:
  val: int = call @rand rng max;
  if_cond: bool = lt val zero;
  br if_cond .if_body .if_done;
.if_body:
  val: int = const 0;
.if_done:
  loc: ptr<int> = ptradd arr i;
  store loc val;
.loop_end:
  i: int = add i one;
  jmp .loop;
.done:
  ret arr;
}
@printarray(size: int, arr: ptr<int>) {
.printarray.b0:
  i: int = const 0;
  one: int = const 1;
.loop:
  cond: bool = lt i size;
  br cond .body .done;
.body:
  loc: ptr<int> = ptradd arr i;
  val: int = load loc;
  print val;
.loop_end:
  i: int = add i one;
  jmp .loop;
.done:
  ret;
}
@zeroarray(size: int): ptr<int> {
.zeroarray.b0:
  arr: ptr<int> = alloc size;
  i: int = const 0;
  zero: int = const 0;
  one: int = const 1;
.loop:
  cond: bool = lt i size;
  br cond .body .done;
.body:
  loc: ptr<int> = ptradd arr i;
  store loc zero;
.loop_end:
  i: int = add i one;
  jmp .loop;
.done:
  ret arr;
}
@adj2csr(num_nodes: int, adjmat: ptr<int>, csr_offset: ptr<int>, csr_edges: ptr<int>): int {
.adj2csr.b0:
  num_edges: int = const 0;
  row: int = const 0;
  one: int = const 1;
.iter_row:
  row_cond: bool = lt row num_nodes;
  col: int = const 0;
  br row_cond .iter_col .row_done;
.iter_col:
  col_cond: bool = lt col num_nodes;
  br col_cond .col_body .col_done;
.col_body:
  row_tmp: int = mul row num_nodes;
  node_idx: int = add row_tmp col;
  node_loc: ptr<int> = ptradd adjmat node_idx;
  node_val: int = load node_loc;
  cond: bool = eq node_val one;
  br cond .if_body .col_end;
.if_body:
  edge_loc: ptr<int> = ptradd csr_edges num_edges;
  store edge_loc col;
  num_edges: int = add num_edges one;
.col_end:
  col: int = add col one;
  jmp .iter_col;
.col_done:
  offset_loc: ptr<int> = ptradd csr_offset row;
  store offset_loc num_edges;
.row_end:
  row: int = add row one;
  jmp .iter_row;
.row_done:
  ret num_edges;
}
@main(num_nodes: int, seed: int) {
.main.b0:
  one: int = const 1;
  rng: ptr<int> = alloc one;
  store rng seed;
  sqsize: int = mul num_nodes num_nodes;
  adjmat: ptr<int> = call @randarray sqsize rng;
  csr_offset: ptr<int> = call @zeroarray sqsize;
  csr_edges: ptr<int> = call @zeroarray sqsize;
  num_edges: int = call @adj2csr num_nodes adjmat csr_offset csr_edges;
  print num_nodes;
  print num_edges;
  call @printarray sqsize adjmat;
  call @printarray num_nodes csr_offset;
  call @printarray num_edges csr_edges;
  free adjmat;
  free csr_offset;
  free csr_edges;
  free rng;
  ret;
}
